plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "xyz.jpenilla.run-paper" version "2.0.1"
}

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "https://repo.codemc.io/repository/maven-snapshots/"
    }
    maven {
        url "https://jitpack.io/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.1"

    compileOnly "com.mojang:authlib:2.1.28"
    compileOnly "com.mojang:datafixerupper:3.0.25"

    implementation "commons-io:commons-io:2.11.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "com.github.robertlit:SpigotResourcesAPI:2.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.0"
    implementation "com.github.retrooper.packetevents:spigot:2.0.0-SNAPSHOT"
}

group "lol.pyr"
version "1.0.8"

sourceCompatibility 1.8
targetCompatibility 1.8

shadowJar {
    archiveClassifier.set ""
    relocate "org.bstats", "lol.pyr.znpcsplus.lib.bstats"
    relocate "org.apache.commons", "lol.pyr.znpcsplus.lib.commons"
    relocate "me.robertlit.spigotresources", "lol.pyr.znpcsplus.lib.spigotresources"
    relocate "net.kyori", "lol.pyr.znpcsplus.lib.kyori"
    relocate "org.checkerframework", "lol.pyr.znpcsplus.lib.checkerframework"
    relocate "javax.annotation", "lol.pyr.znpcsplus.lib.javaxannotation"
    relocate "com.google", "lol.pyr.znpcsplus.lib.google"
    relocate "com.github.retrooper.packetevents", "lol.pyr.znpcsplus.lib.packetevents.api"
    relocate "io.github.retrooper.packetevents", "lol.pyr.znpcsplus.lib.packetevents.impl"
    minimize()
}

processResources {
    expand("version": version)
}

runServer {
    minecraftVersion "1.19.4"
}

tasks.assemble.dependsOn shadowJar
tasks.compileJava.dependsOn clean

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name.set("znpcsplus")
                description.set("A fork of the ZNPCs plugin")
                url.set("https://github.com/Pyrbu/ZNPCsPlus")
            }
        }
    }
    repositories {
        maven {
            if (project.properties.containsKey("DIST_USERNAME") && project.properties.containsKey("DIST_PASSWORD")) {
                credentials {
                    username project.properties.get("DIST_USERNAME")
                    password project.properties.get("DIST_PASSWORD")
                }
            }
            url = uri("https://repo.pyr.lol/releases/")
        }
    }
}
